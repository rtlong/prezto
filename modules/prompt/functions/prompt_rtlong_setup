#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Ryan Long <ryan@rtlong.com>
#

function prompt_rtlong_precmd {
    if (( $+functions[vcs_info] )); then
    fi
    vcs_info 'prompt'

    if (( $+functions[python-info] )); then
        python-info
    fi
}

function prompt_rtlong_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr sp percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_rtlong_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_rtlong_colors=(
      "%F{115}" # Turquoise
      "%F{221}" # Orange
      "%F{182}" # Purple
      "%F{167}" # Hotpink
      "%F{185}" # Limegreen
      "%F{167}" # Red
    )
  else
    _prompt_rtlong_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{red}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_rtlong_colors[1]}%b%f"
  local action_format="${_prompt_rtlong_colors[5]}%a%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}:${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="${_prompt_rtlong_colors[4]}%D %*%f %(?..${_prompt_rtlong_colors[6]}[exit=%?]%f )${_prompt_rtlong_colors[5]}%~%f \${vcs_info_msg_0_}
> "
  RPROMPT=''
}

prompt_rtlong_setup "$@"
